/*
 * yaml-tests.gradle
 *
 * This source file is part of the FoundationDB open source project
 *
 * Copyright 2015-2024 Apple Inc. and the FoundationDB project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: rootProject.file('gradle/proto.gradle')

project.tasks.named("processResources") {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

project.tasks.named("processTestResources") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named("sourcesJar") {
    duplicatesStrategy('include')
    // classifier('sources')
    from sourceSets.main.allSource
}

apply from: rootProject.file('gradle/publishing.gradle')

configurations {
    // The :fdb-relational-grpc project includes the slf4j bindings transitively through :fdb-relational-cli.
    // Excluding it from the individual project seems to be insufficient (and results in a runtime
    // warning about there being multiple implementations present from slf4j), so this applies the
    // exclusion globally
    runtime.exclude group: "org.apache.logging.log4j", module: "log4j-slf4j-impl"
    testImplementation.exclude group: "org.apache.logging.log4j", module: "log4j-slf4j-impl"
    testRuntime.exclude group: "org.apache.logging.log4j", module: "log4j-slf4j-impl"
}

def coreProject = ":${ext.coreProjectName}"
dependencies {
    implementation project(coreProject)
    implementation project(path: coreProject, configuration: 'tests')
    implementation project(":fdb-relational-api")
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation project(":fdb-relational-core")
    implementation "org.yaml:snakeyaml:${snakeyamlVersion}"
    implementation "com.google.code.findbugs:jsr305:${jsr305Version}"
    implementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"

    testImplementation project(":fdb-relational-server")
    testImplementation project(":fdb-relational-jdbc")
    implementation "de.vandermeer:asciitable:${asciitableVersion}"
    implementation "io.github.java-diff-utils:java-diff-utils:4.12"
    // slf4j binding. Allows Record Layer dependencies to log to same place as Relational
    testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    testRuntimeOnly "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

ext.resolveOtherServer = { ->
    def configurationName = "resolveArtifact-${UUID.randomUUID()}"
    return rootProject.with {
        configurations.create(configurationName, {
            resolutionStrategy {
                failOnChangingVersions()
                // Note: In the root of the project we set cacheChangingModulesFor and cacheDynamicVersionFor to 0
                // We explicitly change that for changing modules, because we should reject those anyways.
                cacheChangingModulesFor 1000, "days" // TTL for snapshots
            }
        })
        // I tried setting the version to 'latest.release', but that causes it to download all of the snapshots
        // every time you execute. This is because `latest.release` becomes `LatestVersionSelector` which
        // `requiresMetadata`, because it looks at the metadata for the module to determine if it is a release.
        // I also tried just setting it to `+`, but that causes it to fetch the latest snapshot, which isn't exactly
        // what we want.
        dependencies.add(configurationName,
                ['group': 'org.foundationdb',
                 'name': 'fdb-relational-server',
                 'version': '3.5-SNAPSHOT', // todo: will have to set to a known version once a build is published
                 // the all classifier specifies that we want to fetch the shadow jar, which includes all the
                 // dependencies
                 'classifier': 'all'],
                {
                    // we don't want to take in any transitive dependencies.
                    // There shouldn't be any external dependencies, but commenting this out, this fails with a missing
                    // grpc dependency.
                    transitive=false
                    changing=true
                })
        def configuration = configurations.getByName(configurationName, { })

        def resolution = configuration.resolve()[0]
        System.out.println("Downloaded old external server: " + resolution.getName())
        return resolution
    }
}

task cleanExternalServerDirectory(type: Delete) {
    delete project.layout.buildDirectory.dir('externalServer')
}

task downloadExternalServer(type: Copy) {
    dependsOn "cleanExternalServerDirectory"
    // todo: will be able to uncomment once other server has been published
    // from resolveOtherServer()
    into project.layout.buildDirectory.dir('externalServer')
}

test {
    dependsOn "downloadExternalServer"
    systemProperty("yaml_testing_external_server", project.layout.buildDirectory.dir('externalServer').get().asFile)
    // These are the system properties that are looked at by the YamlTesting framework to initialize the execution context
    var isNightly = System.getenv("COM_APPLE_FOUNDATIONDB_RELATIONAL_YAMLTESTS_NIGHTLY")
    if (isNightly != null) {
        systemProperty("yaml_testing_nightly", isNightly);
    }
    var seed = System.getenv("COM_APPLE_FOUNDATIONDB_RELATIONAL_YAMLTESTS_SEED")
    if (seed != null) {
        systemProperty("yaml_testing_seed", Long.parseLong(seed))
    }
    var maxThreads = System.getenv("COM_APPLE_FOUNDATIONDB_RELATIONAL_YAMLTESTS_MAX_THREADS")
    if (maxThreads != null) {
        systemProperty("yaml_testing_max_threads", Integer.parseInt(maxThreads))
    } else {
        systemProperty("yaml_testing_max_threads", (int) (Runtime.runtime.availableProcessors() / 2))
    }
    var nightlyRepetitions = System.getenv("COM_APPLE_FOUNDATIONDB_RELATIONAL_YAMLTESTS_NIGHTLY_REPETITION")
    if (nightlyRepetitions != null) {
        systemProperty("yaml_testing_nightly_repetition", Integer.parseInt(nightlyRepetitions))
    } else {
        systemProperty("yaml_testing_nightly_repetition", 100)
    }
}

publishing {
    publications {
        library(MavenPublication) {
            pom {
                description = 'Tests of the Relational project driven off of YAML specifications.'
            }
        }
    }
}
