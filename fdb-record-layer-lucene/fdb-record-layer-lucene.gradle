/*
 * fdb-record-layer-lucene.gradle
 *
 * This source file is part of the FoundationDB open source project
 *
 * Copyright 2015-2021 Apple Inc. and the FoundationDB project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: rootProject.file('gradle/proto.gradle')
apply from: rootProject.file('gradle/publishing.gradle')

def coreProject = ":${ext.coreProjectName}"
dependencies {
    api project(coreProject)
    api "org.apache.lucene:lucene-core:${luceneVersion}"
    api "org.apache.lucene:lucene-analyzers-common:${luceneVersion}"
    implementation "org.apache.lucene:lucene-queryparser:${luceneVersion}"
    implementation "org.apache.lucene:lucene-suggest:${luceneVersion}"
    implementation "org.apache.lucene:lucene-highlighter:${luceneVersion}"
    implementation "org.apache.commons:commons-lang3:${commonsLang3Version}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    testImplementation project(path: coreProject, configuration: 'tests')
    testCompileOnly "com.google.code.findbugs:jsr305:${jsr305Version}"
    compileOnly "com.google.auto.service:auto-service:undefined"
    annotationProcessor "com.google.auto.service:auto-service:undefined"
    testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    testRuntimeOnly "org.apache.logging.log4j:log4j-core:${log4jVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:${junitVersion}")
    testImplementation "org.hamcrest:hamcrest:${hamcrestVersion}"
    testImplementation "org.apache.lucene:lucene-test-framework:${luceneVersion}"
    testImplementation project(path: ':fdb-extensions', configuration: 'tests')
    testCompileOnly "com.google.auto.service:auto-service:undefined"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testAnnotationProcessor "com.google.auto.service:auto-service:undefined"
}

// Disable blocking detection, because we do this *a lot* in lucene
test.systemProperties['com.apple.foundationdb.record.blockingInAsyncDetection'] = System.getenv('BLOCKING_DETECTION') ?: "DISABLED"
// Used by lucene tests to create Directory objects for testing our codecs
test.systemProperties['tests.directory'] = 'com.apple.foundationdb.record.lucene.codec.TestFDBDirectory'
// uncomment this line, and comment out the one above to easily test against Lucene's RAMDirectory to see how
// it might differ
// test.systemProperties['tests.directory'] = 'org.apache.lucene.store.RAMDirectory'

// Uncomment below line to run nightly tests
// test.systemProperties['tests.nightly'] = 'true'

tasks.withType(Test) { theTask ->
    if (!project.hasProperty('tests.includeRandom')) {
        println("Fixing seed for lucene tests")
        theTask.systemProperties['tests.seed'] = "C185081D42F0F43C" // a fixed seed, should pass reliably in prb/release
    }
    if (project.hasProperty('tests.iterations')) {
        theTask.systemProperties['tests.iters'] = project.getProperty('tests.iterations')
    }
    if (project.hasProperty('tests.nightly')) {
        theTask.systemProperties['tests.nightly'] = 'true'
    }
}

publishing {
    publications {
        library(MavenPublication) {
            pom {
                description = 'Lucene index support for fdb-record-layer'
            }
        }
    }
}
