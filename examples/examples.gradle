/*
 * examples.gradle
 *
 * This source file is part of the FoundationDB open source project
 *
 * Copyright 2015-2018 Apple Inc. and the FoundationDB project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: rootProject.file('gradle/proto.gradle')
apply plugin: 'application'

def coreProject = ":${ext.coreProjectName}"
dependencies {
    implementation project(coreProject)
    implementation project(':fdb-relational-api')
    implementation project(':fdb-relational-core')
    implementation project(':fdb-relational-jdbc')
    implementation(libs.protobuf)
    implementation(libs.slf4j.api)
    runtimeOnly(libs.log4j.slf4jBinding) // binding
    runtimeOnly(libs.log4j.core) // library
}

application {
    mainClass = 'com.apple.foundationdb.record.sample.Main'
    applicationDefaultJvmArgs = ["-Dlog4j.configurationFile=${projectDir}/src/main/resources/log4j2.properties"]
}

// Disable code quality checks for examples module
// This module contains documentation code snippets that intentionally:
// - Use System.out.println for demonstration purposes
// - Have patterns that trigger false positive resource leak warnings
tasks.withType(Pmd).configureEach {
    enabled = false
}
tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
    enabled = false
}
tasks.withType(Checkstyle).configureEach {
    enabled = false
}
