/*
 * fdb-record-layer-core-shaded.gradle
 *
 * This source file is part of the FoundationDB open source project
 *
 * Copyright 2015-2018 Apple Inc. and the FoundationDB project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.github.johnrengelman.shadow'

def coreProject = ":${ext.coreProjectName}"

dependencies {
  compile project(coreProject)
}

shadowJar {
    classifier = null
    relocate 'com.google', 'com.apple.foundationdb.record.shaded.com.google'
    dependencies {
        include(dependency(coreProject))
        include(dependency('com.google.guava:guava'))
        include(dependency('com.google.protobuf:protobuf-java'))
    }
}

build.dependsOn {
    shadowJar
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            from project.shadow.component(publication)
            publication.pom { pom ->
                pom.withXml { xml ->
                    // Remove any existing dependencies sections
                    def childNodes = xml.asNode().children()
                    def dependenciesNodes = new ArrayList<groovy.util.Node>()
                    childNodes.forEach { childNode ->
                        if (childNode.name().equals('dependencies')) {
                            dependenciesNodes.add(childNode)
                        }
                    }
                    dependenciesNodes.forEach {
                        xml.asNode().remove(it)
                    }
                    // Add a section containing all non-shaded dependencies
                    def dependenciesNode = xml.asNode().appendNode('dependencies')
                    project(coreProject).configurations.compile.getDependencies().forEach {
                        if (!it.name.equals('guava') && !it.name.equals('protobuf-java')) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'compile')
                        }
                    }
                }
            }
        }
    }
    bintray {
        publications = ["shadow"]
    }
}
