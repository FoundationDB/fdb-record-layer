/*
 * proto.gradle
 *
 * This source file is part of the FoundationDB open source project
 *
 * Copyright 2015-2018 Apple Inc. and the FoundationDB project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.google.protobuf'

configurations {
    compileProtoPath {
        // The default generated by com.google.protobuf.gradle.ProtobufPlugin.createCompileProtoPathConfiguration
        // forgets api.
        extendsFrom api, compileOnly, implementation
    }
}

protobuf {
    generatedFilesBaseDir = "${projectDir}/protogen"
    clean {
        delete generatedFilesBaseDir
    }
    protoc {
        artifact = "com.google.protobuf:protoc:${libs.versions.protobuf.get()}"
    }
    generateProtoTasks {
        all().each { task ->
            task.addIncludeDir(files("${projectDir}/src/main/proto/include"))
            if (task.name != 'generateProto') {
                // test protos likely include both main protos and their dependencies.
                task.addIncludeDir(files("${projectDir}/src/main/proto"))
                task.addIncludeDir(files("${projectDir}/.out/extracted-include-protos/main"))
            }

            // When generating from syntax = proto2 files, protoc can generate methods that reference
            // the deprecated PARSER field. Suppress these warnings on generated code
            // See: https://github.com/protocolbuffers/protobuf/issues/7271
            task.doLast {
                task.outputs.getFiles().forEach {
                    ant.replace(dir: it, includes: "**/*.java",
                            token: "public static final class ",
                            value: "@SuppressWarnings(\"deprecation\") public static final class ",
                    )
                }
            }
        }
    }
}

// Configure the published jar to include any external protos
jar {
    from ("${projectDir}/src/main/proto/include") {
        include '**/*.proto'
    }
}
