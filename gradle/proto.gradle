/*
 * proto.gradle
 *
 * This source file is part of the FoundationDB open source project
 *
 * Copyright 2015-2018 Apple Inc. and the FoundationDB project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.google.protobuf'

sourceSets {
    main {
        java {
            srcDir 'protogen/main/java'
        }
    }
    test {
        proto {
            srcDir "src/test/proto${protoMajorVersion}"
        }
        java {
            srcDir 'protogen/test/java'
        }
    }
}

protobuf {
    generatedFilesBaseDir = "${projectDir}/protogen"
    clean {
        delete generatedFilesBaseDir
    }
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generateProtoTasks {
        all().each { task ->
            task.addIncludeDir(files("${projectDir}/src/main/proto/include"))
            task.addIncludeDir(files("${projectDir}/src/main/proto${protoMajorVersion}/include"))
            if (task.name != 'generateProto') {
                task.addIncludeDir(files("${projectDir}/src/main/proto"))
            }
        }
    }
}

// Configure the published jar to include any external protos
jar {
    from ("${projectDir}/src/main/proto/include") {
        include '**/*.proto'
    }
    from ("${projectDir}/src/main/proto${protoMajorVersion}/include") {
        include '**/*.proto'
    }
}
