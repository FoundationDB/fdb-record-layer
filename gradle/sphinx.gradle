/*
 * sphinx.gradle
 *
 * This source file is part of the FoundationDB open source project
 *
 * Copyright 2015-2025 Apple Inc. and the FoundationDB project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

var sphinxRoot = "${rootDir}/docs/sphinx"
var venvDir = "${sphinxRoot}/.venv"
var sphinxBuildDir = "${sphinxRoot}/.out"

task sphinxEnv {
    inputs.file("${sphinxRoot}/requirements.txt")
    outputs.dir(venvDir)

    doLast {
        exec {
            commandLine 'python3', '-m', 'venv', venvDir
        }
        exec {
            commandLine "${venvDir}/bin/pip", 'install', '-r', "${sphinxRoot}/requirements.txt"
        }
    }
}

task generateRailroadDiagrams(type: Exec) {
    inputs.file("${sphinxRoot}/generate_railroad_svg.py")
    inputs.dir("${sphinxRoot}/source")
    outputs.dir("${sphinxRoot}/source")

    dependsOn sphinxEnv
    commandLine "${venvDir}/bin/python", "${sphinxRoot}/generate_railroad_svg.py"
}

// Build the sphinx documentation
task sphinxDocs(type: Exec) {
    inputs.dir(venvDir)
    inputs.dir("${sphinxRoot}/source")
    outputs.dir("${sphinxBuildDir}/html")

    dependsOn sphinxEnv
    dependsOn generateRailroadDiagrams
    commandLine "${venvDir}/bin/sphinx-build", '-M', 'html', "${sphinxRoot}/source", sphinxBuildDir
}

// Clean the sphinx build
task sphinxClean(type: Delete) {
    delete(sphinxBuildDir)
    delete fileTree("${sphinxRoot}/source") {
        include "**/*.diagram.svg"
    }
    delete(venvDir)
}
