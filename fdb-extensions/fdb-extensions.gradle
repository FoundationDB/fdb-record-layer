/*
 * fdb-extensions.gradle
 *
 * This source file is part of the FoundationDB open source project
 *
 * Copyright 2015-2018 Apple Inc. and the FoundationDB project authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java-test-fixtures'
apply from: rootProject.file('gradle/publishing.gradle')

dependencies {
    api(project(":fdb-java-annotations")) {
        exclude(group: "com.squareup", module: "javapoet")
    }
    api(libs.fdbJava)
    implementation(libs.guava)
    implementation(libs.slf4j.api)
    compileOnly(libs.jsr305)

    testImplementation project(':fdb-test-utils')
    testImplementation(libs.bundles.test.impl)
    testImplementation(libs.snakeyaml)
    testRuntimeOnly(libs.bundles.test.runtime)
    testCompileOnly(libs.bundles.test.compileOnly)
    testAnnotationProcessor(libs.autoService)

    testFixturesImplementation(libs.bundles.test.impl)
    testFixturesImplementation(libs.snakeyaml)
    testFixturesCompileOnly(libs.bundles.test.compileOnly)
    testFixturesImplementation(libs.slf4j.api)
    testFixturesAnnotationProcessor(libs.autoService)
}

// Describe all files here
def siftDownloads = [
        siftsmall: [
                url : 'https://huggingface.co/datasets/vecdata/siftsmall/resolve/3106e1b83049c44713b1ce06942d0ab474bbdfb6/siftsmall.tar.gz',
                dest: layout.buildDirectory.file("downloads/siftsmall.tar.gz")
        ],
        sift1mbase: [
                url : 'https://huggingface.co/datasets/qbo-odp/sift1m/resolve/main/sift_base.fvecs',
                dest: layout.buildDirectory.file("downloads/sift_base.fvecs")
        ],
        sift1mgroundtruth: [
                url : 'https://huggingface.co/datasets/qbo-odp/sift1m/resolve/main/sift_groundtruth.ivecs',
                dest: layout.buildDirectory.file("downloads/sift_groundtruth.ivecs")
        ],
        sift1mquery: [
                url : 'https://huggingface.co/datasets/qbo-odp/sift1m/resolve/main/sift_query.fvecs',
                dest: layout.buildDirectory.file("downloads/sift_query.fvecs")
        ],
]

// Register one Download task per entry
def downloadTasks = siftDownloads.collect { name, cfg ->
    tasks.register("download${name.capitalize()}", Download) {
        src cfg.url
        dest cfg.dest.get().asFile
        onlyIfModified true
        tempAndMove true
        retries 3
        overwrite false
        outputs.file(dest)
    }
}

def extractDir = layout.buildDirectory.dir("extracted")

tasks.register('extractSiftSmall', Copy) {
    dependsOn downloadTasks
    from(tarTree(resources.gzip(siftDownloads.siftsmall.dest)))
    into extractDir

    doLast {
        println "Extracted files into: ${extractDir.get().asFile}"
        fileTree(extractDir).visit { details ->
            if (!details.isDirectory()) {
                println " - ${details.file}"
            }
        }
    }
}

test {
    dependsOn downloadTasks
    dependsOn tasks.named('extractSiftSmall')
    inputs.dir extractDir
}

publishing {
    publications {
        library(MavenPublication) {
            pom {
                description = 'Extensions to the FoundationDB Java API.'
            }
        }
    }
}
